apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'kotlin-kapt'

android {

    useLibrary 'org.apache.http.legacy'

    compileSdkVersion target_sdk

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion min_sdk_rx
        targetSdkVersion target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation project("${dbflow_project_prefix}lib")
    implementation project("${dbflow_project_prefix}sqlcipher")
    implementation project("${dbflow_project_prefix}reactive-streams")
    implementation project("${dbflow_project_prefix}contentprovider")
    implementation project("${dbflow_project_prefix}coroutines")
    implementation project("${dbflow_project_prefix}paging")
    implementation project("${dbflow_project_prefix}livedata")

    kaptAndroidTest project("${dbflow_project_prefix}processor")

    androidTestImplementation deps.javax_annotation
    androidTestImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-RC3") {
        exclude group: "org.jetbrains.kotlin"
    }
    androidTestImplementation "org.mockito:mockito-android:2.23.0"

    androidTestImplementation deps.junit
    androidTestImplementation "androidx.test:core:1.0.0"
    androidTestImplementation "androidx.test:runner:1.1.0"
    androidTestImplementation "androidx.test:rules:1.1.0"
    androidTestImplementation "androidx.arch.core:core-testing:2.0.0"

}

android.applicationVariants.all { variant ->
    String taskName = "copy${variant.name.capitalize()}ResDirectoryToClasses"
    task(taskName, type: Copy) {
        from "${projectDir}/src/test/res"
        into "${buildDir}/intermediates/classes/test/${variant.buildType.name}/res"

        from "${projectDir}/src/test/assets"
        into "${buildDir}/intermediates/classes/test/${variant.buildType.name}/assets"
    }
    project.getTasksByName("generate${variant.name.capitalize()}Resources", false)[0].dependsOn(taskName)
}

dexcount {
    includeClasses = true
    orderByMethodCount = true
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-XXLanguage:+InlineClasses"]
}